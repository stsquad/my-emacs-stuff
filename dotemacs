; Generic .emacs, written in -*- emacs-lisp -*-
; Time-stamp: <18:36 on Wednesday, 2 February 2005 by alexjb>
;

(message ".emacs")

(message "Start")


(setq debug-on-error t)

;;;; Start of real code.

;;; Basic sanity
;; Find out about my environment
;; Tweak a few paths

(defvar I-am-emacs-21 (string-match "GNU Emacs 21" (emacs-version)))

;; Lets define which machine I'm on, therefor if I am at work
;; (this of course falls down when logging on remotely, but hey I
;; always do that in text mode anyway :-)
 
(defvar I-am-at-work (string-match "cambridge" (system-name)))
(defvar I-am-at-home (string-match "trent" (system-name)))

;; (setq load-path (cons "~/lisp/" load-path))

;; Autoloads
;? Put them near their configuration.
;? Check that extended modes are available.
(autoload 'msb-mode "msb" "Fancier buffer-selection menu" t)

(if (locate-library "dict-web")
    (autoload 'dict "dict-web" "Look up a word or phrase in the Dictionary" t))

(if (locate-library "babel")
    (progn
      (autoload 'babel "babel"
	"Use web translation site to translate message" t)
      (autoload 'babel-region "babel" "Translate the current region." t)
      (autoload 'babel-as-string "babel"
	"Translate MSG, returning a string." t)
      (autoload 'babel-buffer "babel" "Translate the current buffer." t)))

;(require 'generic-x)  ; Includes ini-generic-mode

;; Looks like Ed was bored
(if (locate-library "pyf")
    (autoload 'pick-your-fish "pyf" "Pick Your Fish!" t))
(if (locate-library "mc")
    (autoload 'mc             "mc"  "Mornington Crescent" t))

;; Default mode is text-mode,
(setq default-major-mode 'text-mode)

;; Don't truncate message buffer. For debugging reasons.
(setq message-log-max t)

;; Less verbosity
(fset 'yes-or-no-p 'y-or-n-p)

;; Prompt for Local Variable lists
(setq enable-local-variables 'maybe)

;; Silently add trailing newline to file
(setq require-final-newline t)

;; ^K deletes line, not delete to EOL
(setq-default kill-whole-line t)

;; Searches are case sensitive
(setq-default case-fold-search nil)

;; Automagically decompress files
(auto-compression-mode)

;; Change menu entry so it doesn't use faces.
(define-key global-map [menu-bar tools print ps-print-buffer]
  '("Postscript Print Buffer" . ps-print-buffer))

(message "Done Basic Sanity")


;;; Miscellaneous functions

(defun praise-emacs ()
  (interactive)
  (message "Basking in the glory of your praise...")
  (sleep-for 1)
  (message "Basking in the glory of your praise...Done."))

(defun fortune ()
  "Famous unix fortune teller."
  (shell-command-to-string "/usr/games/fortune"))

(defun my-kill-line ()
  "Iain's hack to make kill-line work even when you're in the middle of one."
  (interactive)
  (beginning-of-line)
  (call-interactively 'kill-line))


(message "Done defuns")


;;; keymapping
;; I'm allowed to bind: C-c [letter], [f5]-[f9]

;; Goto-line should be easy
(global-set-key "\C-cg" 'goto-line)
(global-set-key "\M-g" 'goto-line)

;; Return => newline-and-indent
(global-unset-key "\C-m")
(global-set-key "\C-m" 'newline-and-indent)

;; Make delete do what I expect
;? Do I still need this?
(global-unset-key [delete])
(global-set-key [delete] 'delete-char)

;; C-k deletes whole line
(global-unset-key "\C-k")
(global-set-key "\C-k" 'my-kill-line)

;; In Emacs 21, home and end go to beginning and end of line. This is
;; clearly the Wrong Thing.
(if I-am-emacs-21
    (progn ()
	   (global-unset-key [home])
	   (global-set-key [home] 'beginning-of-buffer)
	   (global-unset-key [end])
	   (global-set-key [end] 'end-of-buffer)
	   ))

(message "Done keymapping")


(message "Setting up display")

;;; Display hacks
;;; This actually works, except of course the frame has already been created
;;(setq default-x-frame-alist '((top . 20) (left . 140)
;;			    (width . 90) (height . 80)))

;; Set the frame size to larger if on a Graphics display (we tell by
;; looking at colour cells (must fix to not break emacs20, maybe)
(when (> (display-color-cells) 16)
  (if I-am-at-work
      (progn
	(set-frame-width (selected-frame) 120)
	(set-frame-height (selected-frame) 60))
;;smaller monitor at home :-(
    (set-frame-width (selected-frame) 120)
    (set-frame-height (selected-frame) 50)))
    

(message "Display Done")

;; Disable tool-bar (I never use it)
(tool-bar-mode -1)

;; fancier buffer menu
(setq buffers-menu-max-size nil) ; Don't think this is needed with msb
(msb-mode 1)

;; Prettier unique buffer names.
(require 'uniquify)
(setq uniquify-buffer-name-style 'post-forward-angle-brackets)

;; Stop the mouse cursor getting in the way. This is great.
(if 'window-system
    (mouse-avoidance-mode 'cat-and-mouse))

;; Change the cursor colour in Ovwrt mode
(defun ins-cursor-set ()
  "Set cursor colour according to insert mode"
  (set-cursor-color
   (if overwrite-mode
       "red"
     "black")))

(if I-am-emacs-21
    (blink-cursor-mode -1))

(add-hook 'post-command-hook 'ins-cursor-set)

(setq frame-title-format "%b")
(setq  icon-title-format "%b")

;(if I-am-emacs-21
;    (progn
;      (autoload 'zone-when-idle "zone")
;      (zone-when-idle 60)))

;; want to reduce the amount of white space in the mode-line
(setq default-mode-line-format
      '("-"
	mode-line-mule-info
	mode-line-modified
	" "
	mode-line-buffer-identification
	" "
	global-mode-string
	"%[("
	mode-name
	mode-line-process
	minor-mode-alist
	"%n"
	")%]-"
	(line-number-mode "L%l-")
	(column-number-mode "C%c-")
	(which-func-mode ("" which-func-format))
	"-%-"))

;; Let's shrink the minor-mode-alist down to size.
(setcdr (assq 'abbrev-mode minor-mode-alist) '(" Ab"))
(setcdr (assq 'auto-fill-function minor-mode-alist) '(" Fl"))

;; Not added until the relevant mode is loaded.
(setq minor-mode-alist (cons '(compilation-in-progress nil)
				 minor-mode-alist))

;; Uses a separate variable. Isn't that nice?
(setq eldoc-minor-mode-string nil
      highlight-changes-passive-string nil)

;; (display-time) is needed for appt to display in the mode-line, but
;; we don't want the time taking up precious space.
(setq display-time-24hr-format t
      display-time-interval    20
      display-time-format "")
(display-time)

;; Displays current function() in programming modes. 
(setq which-func-modes t
      which-func-format '("[" which-func-current "]-"))
(which-func-mode)

;; Reduce white space
(setq-default mode-line-buffer-identification '("%b"))

;; Does exactly what it says on the tin
(resize-minibuffer-mode t)

;; Make fill do the Right Thing with full-stops.
(setq sentence-end-double-space nil)

;; Highlights region _all_ the time. Slightly buggy...
(transient-mark-mode t)
(delete-selection-mode 1)

;; Groovy things with matching parentheses
(show-paren-mode t)

;; Tweaks to scrolling behaviour. Still a bit odd.
(setq scroll-preserve-screen-position t
      scroll-conservatively 5
      scroll-step 1
      next-line-add-newlines nil)
(set-scroll-bar-mode 'right)

;; Make pound signs work
(set-language-environment "Latin-1")
(setq unibyte-display-via-language-environment t)

;; Allow narrowing.
(put 'narrow-to-region 'disabled nil)

(message "Done Display Hacks")


;;; Minor modes
;; Highlight changes.
;? Timer-based M-x highlight-changes-rotate-face
(setq highlight-changes-colours '("red1" "red2" "red3" "red4"))
(global-highlight-changes 'passive)

;; Problem is, this only appears to work when h-c-m is 'active. And it
;; sort of clashes with font-lock then.
(add-hook 'highlight-changes-enable-hook
	  '(lambda ()
	     (add-hook 
	      'local-write-file-hooks 'highlight-changes-rotate-faces)))

(add-hook 'highlight-changes-disable-hook
	  '(lambda ()
	     (remove-hook 
	      'local-write-file-hooks 'highlight-changes-rotate-faces)))

;; Expands a time-stamp line
(setq time-stamp-format "%02H:%02M on %:a, %:d %:b %:y by %u")
(add-hook 'write-file-hooks 'time-stamp)

;; HTMLize
;? Buggy.
(if (locate-library "htmlize")
    (progn
      (setq htmlize-output-type 'font
	    htmlize-use-rgb-map nil)	;? (if (not I-am-win32 t)?
      (autoload 'htmlize-buffer "htmlize" "Convert buffer to HTML" t)
      (autoload 'htmlize-file "htmlize" "Convert file to HTML and save" t)
      (autoload 'htmlize-many-files "htmlize" "Convert files to HTML" t)
      (autoload 'htmlize-many-files-dired
	"htmlize" "Convert files to HTML" t)))

;; Auto-Insert
(auto-insert-mode 1)
(setq auto-insert-alist ())		;? html-helper

;; Speedbar (not that I use it much)
(add-hook 'speedbar-load-hook
	  '(lambda ()
	     (setq speedbar-update-speed 5
		   speedbar-track-mouse-flag t
		   speedbar-activity-change-focus t)))

;; Bow down before font-lock
(add-hook 'font-lock-mode-hook
	  '(lambda ()
	     (setq font-lock-maximum-decoration  t
		   font-lock-verbose             t
		   font-lock-support-mode
		                   (if I-am-emacs-21
				       'jit-lock-mode
				     'lazy-lock-mode)
		   lazy-lock-defer-on-scrolling  nil
		   lazy-lock-defer-contextually  t
		   lazy-lock-stealth-verbose     t
		   lazy-lock-stealth-lines       50
		   lazy-lock-stealth-time        3)))
(global-font-lock-mode t)

;; Set the colours I want
;; (loosely based on Mandrake's theme)

(set-background-color "DarkSlateGrey")
(set-foreground-color "wheat")




;;; Major non-file-related modes
;; TRAMP
(if (locate-library "tramp")
    (progn
      (load-library "tramp")
      ))

;; Calendar / Diary stuff.

(setq calendar-latitude      53.30
	calendar-longitude     -2.15
	calendar-location-name "Manchester")
 

(setq european-calendar-style t
      mark-holidays-in-calendar t
      mark-diary-entries-in-calendar t
      all-christian-calendar-holidays t
      all-hebrew-calendar-holidays t
      all-islamic-calendar-holidays t

      appt-audible nil
      appt-display-duration 10
      appt-message-warning-time 8
      appt-display-interval 4)

(add-hook 'diary-hook 'appt-make-list)	;? When does this happen?
(add-hook 'diary-display-hook 'fancy-diary-display)
(add-hook 'list-diary-entries-hook
	  '(lambda ()
	     (sort-diary-entries)
	     (include-other-diary-files)))
(add-hook 'mark-diary-entries-hook 'mark-included-diary-files)

(if (locate-library "todo-mode")
    (progn
      (autoload 'todo-mode "todo-mode" "Major mode for editing TODO lists." t)
      (autoload 'todo-show "todo-mode" "Show TODO items." t)
      (autoload 'todo-insert-item "todo-mode" "Add TODO item." t)
      
      (setq todo-print-function 'ps-print-buffer
	    todo-save-top-priorities-too nil
	    todo-time-string-format "[%02d/%02m/%:y]"
	    todo-entry-prefix-function 'my-todo-entry-timestamp-initials)

      (defun my-todo-entry-timestamp-initials ()
	(let ((time-stamp-format todo-time-string-format))
	  (concat (time-stamp-string) " ")))))

;; Font locking info mode (from Andy.Ling@quantel.com)
(defvar info-font-lock-keywords
  (list
   '("^\\* [^:]+:+" . font-lock-function-name-face)
   '("\\*[Nn]ote\\b[^:]+:+" . font-lock-reference-face)
   '("  \\(Next\\|Prev\\|Up\\):" . font-lock-reference-face))
  "Additional expressions to highlight in Info mode")

(add-hook 'Info-mode-hook
	  (lambda ()
	    (make-local-variable 'font-lock-defaults)
	    (setq
;	          font-lock-defaults '(info-font-lock-keywords nil t)
		  case-fold-search nil)))

;; ediff
(setq diff-switches               "-u"
      ediff-custom-diff-options   "-U3"
      ediff-split-window-function 'split-window-horizontally
      ediff-window-setup-function 'ediff-setup-windows-plain
      ediff-use-last-dir          t)

(add-hook 'ediff-startup-hook 'ediff-toggle-wide-display)

(add-hook 'ediff-cleanup-hook 'ediff-toggle-wide-display)

(add-hook 'ediff-suspend-hook 'ediff-toggle-wide-display)
;? Also need to find a way to restore it all on
;  resume. This stuff is all far from bullet-proof.

(message "Doing ispell dict setting")

;; ispell
;
; There should be an easier way to set the default
; however I'm currently setting each time a file
; is opened using the find-file-hooks

; I'm British, not Amercian damit!
(defun set-british-dict ()
  "Set British Dictionary"
  (ispell-change-dictionary "british")
  (message "Set ispell to British Dictionary"))

; set British dict for all files
(add-hook 'find-file-hooks 'set-british-dict)

; if we are editing text turn on flyspell mode
(add-hook 'text-mode-hook 'flyspell-mode)


;; Compile mode. Or rather, comint, compile and mode-compile.
(setq compilation-scroll-output t
      compile-auto-highlight    t
      compilation-window-height 9)

(if (locate-library "mode-compile")
    (progn
      (autoload 'mode-compile "mode-compile"
	"Compile file in current buffer" t)))

;; Version control library. Splendid.
(setq vc-command-messages t
      vc-initial-comment t)

;; Elisp list
(if (locate-library "ell")
    (progn
      (setq ell-locate t
	    ell-goto-addr t)
      (cond
       (I-am-at-work			;? And for home?
	  (setq ell-proxy-host "proxy.gpt.co.uk"
		ell-proxy-port 8080)))
      (autoload 'ell-packages "ell" "Display the Emacs lisp list" t)))

;; WoMan - WithOut Man
(if (locate-library "woman")
    (progn
      (autoload 'woman "woman" "Decode and browse a UN*X man page." t)
      (autoload 'woman-find-file "woman" "Decode UN*X man-page file." t)
      (autoload 'woman-dired-find-file "woman" "Browse man page from dired" t)

      (setq woman-imenu-title "Imenu"
	    woman-imenu t
	    woman-use-own-frame nil)
      
      (add-hook 'dired-mode-hook
		(lambda ()
		  (define-key dired-mode-map "W" 'woman-dired-find-file)))))

;; Dired stuff
(add-hook 'dired-mode-hook
		(lambda ()
		  (setq truncate-lines t)
		  (auto-show-mode 1)))

;; W3 + browse URL
;? Variable to customise the buffer it appears in?
;? Needs work for -home
(if (locate-library "w3-auto")
    (progn
      (require 'w3-auto)
      (setq w3-do-incremental-display   t
	    w3-user-fonts-take-precedence t
	    url-be-asynchronous         nil
	    url-cookie-confirmation     t
	    url-privacy-level 'paranoid)

      (setq w3-default-homepage
	    "http://www-comms-cov.intranet.marconi.com/home/"
	    url-proxy-services
	    '(("http" ."proxy.gpt.co.uk:8080")
	      ("ftp" . "proxy.gpt.co.uk:8080")
	      ("no_proxy" .
    "^.*\\(marconicomms\.com\\)\\|\\(marconi\.com\\)\\|\\(gpt\.co\.uk\\)")))
      
      (add-hook 'url-local-hook 'url-setup-privacy-info)
      (add-hook 'w3-mode-hook
		'(lambda ()
		   (auto-show-mode 1))))) ; Horizontal scrolling

;; Vcard - for gnus MIME
(if (locate-library "vcard")
    (setq vcard-pretty-print-function 'vcard-format-sample-string))


;;; File modes
;; New modes - add (imenu-add-to-menubar "Imenu") and (turn-on-auto-fill)
;;? Or should I put (turn-on-auto-fill) in find-files-hook?

;; cc-mode
(add-hook 'c-mode-common-hook
	  '(lambda ()
	     (defconst my-c-style
	       '((c-tab-always-indent . nil)
		 (c-indent-level 3)
		 (c-comment-only-line-offset 0)
		 (c-basic-offset . 4)
;		 (c-echo-syntactic-information-p . t)
		 (c-electric-pound-behavior . (alignleft))
		 (c-hanging-comment-ender-p . nil)
		 (c-comment-continuation-stars . "* ")
		 (c-recognize-knr-p . nil)
		 (c-cleanup-list . (empty-defun-braces
				    defun-close-semi
				    list-close-comma
				    scope-operator))
		 (c-hanging-braces-alist . ((brace-list-open)
					    (brace-list-close)
					    (block-close . c-snug-do-while)
					    (substatement-open before after)))
		 (c-hanging-colons-alist . ((member-init-intro after)
					    (access-label after)
					    (inher-intro after)
					    (case-label after)
					    (label after)))
		 (c-offsets-alist . ((arglist-close . c-lineup-arglist)
				     (substatement-open . 0)
				     (statement-cont . ++)
				     (arglist-cont-nonempty . ++)
;				     (ansi-funcdecl-cont . 0)
				     (case-label . +)
				     (block-open . 0)))
		 )
	       "Alex's C style")

	     (c-add-style "Alex's C style" my-c-style t)
	     (turn-on-auto-fill)
; no auto new line	     (c-toggle-auto-state 1)
	     (imenu-add-to-menubar "Imenu")
	     (if I-am-emacs-21
		 (cwarn-mode))
	     ))

;; Obey local variables set in -*- type things (maybe only for C files
;; though?)
(setq enable-local-variables t)

;; Set proper values for kernel work
(defun linux-c-mode ()
  "C mode with adjusted defaults for use with the Linux kernel."
  (interactive)
  (c-mode)
  (c-set-style "K&R")
  (setq c-basic-offset 8))

;; if its got linux in the path then its a kernel file
(setq auto-mode-alist (cons '("*/linux.*/.*\\.[ch]$" . linux-c-mode)
                       auto-mode-alist))

;; load the ctags library and bind C-f to something worth using :-)
(if (locate-library "etags")
    (progn
      (load-library "etags")
      (global-unset-key "\C-f")
      (global-set-key "\C-f" 'find-tag)))


;; tcl
(add-hook 'tcl-mode-hook
	  '(lambda ()
	     (turn-on-auto-fill)
	     (imenu-add-to-menubar "Imenu")
	     ))

;; CPerl-mode
(setq interpreter-mode-alist (append (list (cons "perl" 'cperl-mode)
					   (cons "perl5" 'cperl-mode)
					   (cons "miniperl" 'cperl-mode))
				     interpreter-mode-alist))

(setq auto-mode-alist (append (list 
			       (cons "\\.\\([pP][Llm]\\|al\\)\\'" 'cperl-mode)
			       (cons "\\.plx\\'" 'cperl-mode)
			       (cons "\\.cgi\\'" 'cperl-mode)
			       (cons "\\.pod\\'" 'cperl-mode)
			       (cons "/home/iain/perl/.*" 'cperl-mode))
			      auto-mode-alist))

(setq cperl-info-on-command-no-prompt nil
      cperl-clobber-lisp-bindings     nil
      cperl-electric-parens           nil
      cperl-electric-keywords         nil)
					 ; cperl-mode tries to load
					 ; abbrev before running the hook

(add-hook 'cperl-mode-hook
	  '(lambda ()
	     (cperl-set-style "BSD")
	     (setq cperl-hairy                                 t
	           cperl-merge-trailing-else                   nil
	           cperl-tab-always-indent                     nil
	           cperl-auto-newline                          nil
	           cperl-electric-lbrace-space                 nil
	           cperl-electric-linefeed                     t
	           cperl-electric-parens                       nil
	           cperl-electric-keywords                     nil
	           cperl-lazy-help-time                        1
	           cperl-extra-newline-before-brace            t
	           cperl-extra-newline-before-brace-multiline  t
	           cperl-max-help-size                         50)
	     (turn-on-auto-fill)
	     (imenu-add-to-menubar "Imenu")
	     (if (not cperl-lazy-installed)	; Only toggle if it's
		 (cperl-toggle-autohelp))	; not already set
	     (if (locate-library "mode-compile")
		 (define-key cperl-mode-map "\C-cr" 'mode-compile))
	     (define-key cperl-mode-map "\C-cc" 'cperl-check-syntax)
	     (define-key cperl-mode-map "\C-j"  'cperl-linefeed)
	     (message "Ran cperl-mode hook")))


;; Elisp mode
(setq auto-mode-alist
      (append (list (cons "\\.emacs\\'" 'emacs-lisp-mode))
	      auto-mode-alist))

(add-hook 'emacs-lisp-mode-hook
	  '(lambda ()
	     (eldoc-mode t)
	     (turn-on-auto-fill)
	     (imenu-add-to-menubar "Imenu")))

(setq tex-default-mode 'latex-mode)	;? Doesn't work.
(add-hook 'tex-mode-hook
	  '(lambda ()
	     (turn-on-auto-fill)
	     (setq tex-dvi-print-command "dvips"
		   tex-dvi-view-command 
		   	(if (eq window-system 'x)
			    "xdvi"
			  "dvi2tty * | cat -s"))))
(add-hook 'latex-mode-hook
	  '(lambda ()
	     (imenu-add-to-menubar "Imenu")))

;; html-helper
;;? See skeleton-insert & auto-insert-alist
;? if new file && /\.hitop$/
;	     (if (eq 0 (string-match "/home/iain/public_html"
;				     buffer-file-name))
;		 (insert "<SET NAME=\"TITLE\"  VALUE=\"Title\">\n"
;			 "<SET NAME=\"BANNER\" VALUE=\"Banner\">\n\n"
;			 "<DEF NAME=\"MAIN\">\n\n</DEF>\n\n"
;			 "<FILE SRC=\"${RELPATH}template.hitop\">\n"))

(if (locate-library "html-helper-mode")
    (progn
      (autoload 'html-helper-mode "html-helper-mode" "Enhanced HTML mode" t)

      (setq auto-mode-alist
	    (append (list (cons "\\.s?html?\\'"  'html-helper-mode)
			  (cons "\\.hitop\\'" 'html-helper-mode)
			  (cons "\\.live\\'" 'html-helper-mode)
			  ;;? What about a hitop-mode?
			  (cons "\\.ht\\'"    'html-helper-mode))
		    auto-mode-alist)
	    html-helper-mode-uses-visual-basic nil
	    html-helper-address-string user-mail-address)

      (add-hook 'html-helper-load-hook
		'(lambda ()
		   (if (locate-library "html-helper-imenu")
		       (progn
			 (autoload 'html-helper-imenu-setup
			   "html-helper-imenu")
			 (setq html-helper-imenu-title "Imenu")
			 (html-helper-imenu-setup)))

		   (setq tempo-interactive t
			 html-helper-build-new-buffer nil)

		   (defun my-html-helper-timestamp ()
		     (let ((time (current-time-string)))
		       (insert (substring time 4 11)
			       (substring time -4) " ")))
		   (setq html-helper-timestamp-hook 'my-html-helper-timestamp)
		   ))

      (add-hook 'html-helper-mode-hook
		'(lambda ()
		   (setq html-helper-basic-offset 4)
		   
		   (set
		    (make-local-variable 'time-stamp-format)
		    "%:d-%:m-%:y")

		   ;;? Add hitop tags to html-helper-types-to-install?
		   ;;? This looks quite hairy
		   (turn-on-auto-fill)))))

(add-hook 'text-mode-hook
	  '(lambda ()
	     (turn-on-auto-fill)))

(add-hook 'texinfo-mode-hook
	  '(lambda ()
	     (imenu-add-to-menubar "Imenu")))

;; PHP file mode
(if (locate-library "php-mode-102")
    (progn
      (load-library "php-mode-102")
      ;; colourization
      (add-hook 'php-mode-user-hook 'turn-on-font-lock)))

;; Batch file mode
(setq auto-mode-alist
      (append (list (cons "\\.cmd\\'" 'bat-generic-mode)
		    (cons "\\.nt\\'" 'bat-generic-mode))
	      auto-mode-alist))

(add-hook 'snmp-common-mode-hook
	  '(lambda ()
	     (make-variable-buffer-local 'make-backup-files)
	     (setq make-backup-files nil)
	     (imenu-add-to-menubar "Imenu")))

;; enable the mouse wheel
(autoload 'mwheel-install "mwheel" "Enable wheely mouse")
(mwheel-install)

;; Convert dos to unix text files
;; from: elf@ee.ryerson.ca (Luis Fernandes)
;; 22 May 1997

;;; Usage: M-x dos2unix
;;;
(defun dos2unix ()
  "Convert this entire buffer from MS-DOS text file format to UNIX."
  (interactive)
  (save-excursion
    (goto-char (point-min))
    (replace-regexp "\r$" "" nil)
    (goto-char (1- (point-max)))
    (if (looking-at "\C-z")
        (delete-char 1))))

;;; Fancy buffer cycleing

; necessary support function for buffer burial
(defun crs-delete-these (delete-these from-this-list)
  "Delete DELETE-THESE FROM-THIS-LIST."
  (cond
   ((car delete-these)
    (if (member (car delete-these) from-this-list)
	(crs-delete-these (cdr delete-these) (delete (car delete-these)
                                                     from-this-list))
      (crs-delete-these (cdr delete-these) from-this-list)))
   (t from-this-list)))

; this is the list of buffers I never want to see
(defvar crs-hated-buffers
  '("KILL" "*Compile-Log*"))

; might as well use this for both
(setq iswitchb-buffer-ignore (append '("^ " "*Buffer") crs-hated-buffers))

(defun crs-hated-buffers ()
  "List of buffers I never want to see, converted from names to buffers."
  (delete nil
	  (append
	   (mapcar 'get-buffer crs-hated-buffers)
	   (mapcar (lambda (this-buffer)
		     (if (string-match "^ " (buffer-name this-buffer))
			 this-buffer))
		   (buffer-list)))))

; I'm sick of switching buffers only to find KILL right in front of me
(defun crs-bury-buffer (&optional n)
  (interactive)
  (unless n
    (setq n 1))
  (let ((my-buffer-list (crs-delete-these (crs-hated-buffers)
					  (buffer-list (selected-frame)))))
    (switch-to-buffer
     (if (< n 0)
	 (nth (+ (length my-buffer-list) n)
	      my-buffer-list)
       (bury-buffer)
       (nth n my-buffer-list)))))

(global-set-key [(control tab)] 'crs-bury-buffer)
(global-set-key [(control meta tab)] (lambda ()
				       (interactive)
				       (crs-bury-buffer -1)))
;;; Bind control-tab in a cycle buffer kind of way
;;(global-set-key [(control tab)] 'bury-buffer)

;; More keybindings, make Control-c copy ala windoze - probably a bad idea
;;(global-set-key [(control c)] 'kill-ring-save)

;; If I define a single press macro exec I may use it more often
(global-set-key [(control meta m)] 'call-last-kbd-macro)

(message "Done .emacs")

(setq debug-on-error nil)
